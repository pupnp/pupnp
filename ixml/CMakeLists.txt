#SUBDIRS 		= doc

ADD_SUBDIRECTORY (test)

SET (IXML_SOURCES
	src/attr.c
	src/document.c
	src/element.c
	src/ixml.c
	src/ixmldebug.c
	src/ixmlmembuf.c
	src/ixmlparser.c
	src/namedNodeMap.c
	src/node.c
	src/nodeList.c
)

ADD_LIBRARY (ixml_shared SHARED
	${IXML_SOURCES}
)

ADD_LIBRARY (IXML::SHARED ALIAS ixml_shared)

TARGET_COMPILE_DEFINITIONS (ixml_shared
	PRIVATE $<IF:$<CONFIG:Debug>,DEBUG,NDEBUG>
	PRIVATE $<$<BOOL:${script_support}>:IXML_HAVE_SCRIPTSUPPORT>
	PUBLIC $<$<BOOL:${MSVC}>:UPNP_USE_MSVCPP>
	PUBLIC $<$<BOOL:${MSVC}>:LIBUPNP_EXPORTS>
)

TARGET_INCLUDE_DIRECTORIES (ixml_shared
	PRIVATE ${CMAKE_CURRENT_SOURCE_DIR}/src/inc/
	PRIVATE ${UPNP_BINARY_DIR}/$<CONFIG>/
	PRIVATE ${UPNP_SOURCE_DIR}/upnp/inc/
	PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/inc/>
)

LIST (APPEND IXML_HEADERS
	inc/ixml.h
	inc/ixmldebug.h
)

SET_TARGET_PROPERTIES(ixml_shared PROPERTIES
	DEBUG_POSTFIX ${CMAKE_DEBUG_POSTFIX}
	OUTPUT_NAME ixml
	EXPORT_NAME IXML::SHARED
	VERSION ${IXML_VERSION}
	SOVERSION ${IXML_VERSION_MAJOR}
	PUBLIC_HEADER "${IXML_HEADERS}"
)

INSTALL (TARGETS ixml_shared
	EXPORT IXMLCONFIG
	DESTINATION ${CMAKE_INSTALL_LIBDIR}
	INCLUDES DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/upnp
	PUBLIC_HEADER DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/upnp
)

ADD_LIBRARY (ixml_static STATIC
	${IXML_SOURCES}
)

ADD_LIBRARY (IXML::STATIC ALIAS ixml_static)

SET_TARGET_PROPERTIES(ixml_static PROPERTIES
	DEBUG_POSTFIX ${CMAKE_DEBUG_POSTFIX}
	OUTPUT_NAME ixml
	EXPORT_NAME IXML::STATIC
)

TARGET_COMPILE_DEFINITIONS (ixml_static
	PRIVATE UPNP_STATIC_LIB
	PRIVATE $<IF:$<CONFIG:Debug>,DEBUG,NDEBUG>
	PRIVATE $<$<BOOL:${script_support}>:IXML_HAVE_SCRIPTSUPPORT>
	PUBLIC $<$<BOOL:${MSVC}>:UPNP_USE_MSVCPP>

)

IF (script_support)
	TARGET_COMPILE_DEFINITIONS (ixml_static
		PRIVATE IXML_HAVE_SCRIPTSUPPORT
	)
ENDIF (script_support)

TARGET_INCLUDE_DIRECTORIES (ixml_static
	PRIVATE ${CMAKE_CURRENT_SOURCE_DIR}/src/inc/
	PRIVATE ${UPNP_BINARY_DIR}/$<CONFIG>/
	PRIVATE ${UPNP_SOURCE_DIR}/upnp/inc/
	PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/inc/>
)

INSTALL (TARGETS ixml_static
	EXPORT IXMLCONFIG
	DESTINATION ${CMAKE_INSTALL_LIBDIR}
	INCLUDES DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/upnp
)

INSTALL (EXPORT IXMLCONFIG
	DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake
)
